# SPDX-License-Identifier: GPL-3.0-or-later
#
# Copyright 2012, 2013, 2014 Kai Pastor (OpenOrienteering)
#
# This file is part of LibreMapper.

# Synopsis:
#   cmake [ -DPREFIX=my/prefix ] [ -DARCHIVE_NAME=prefix-src ]
#         [ -DOUTPUT_DIR=/usr/src ] [ -DSKIP_CHECK=1 ] -P Mapper_Source.cmake

if (NOT PREFIX)
	set(PREFIX "@Mapper_Source_PREFIX@")
endif ()

if (NOT ARCHIVE_NAME)
	set(ARCHIVE_NAME "@Mapper_Source_FILE_NAME@")
endif ()

if (NOT OUTPUT_DIR)
	set(OUTPUT_DIR "@PROJECT_BINARY_DIR@")
endif ()
if (NOT OUTPUT_DIR)
	set(OUTPUT_DIR .)
endif ()

if (NOT SKIP_CHECK)
	# Check that the working directory is clean.
	set(ENV{LANG} C)
	execute_process(
	  COMMAND "@GIT_EXECUTABLE@" status
	  WORKING_DIRECTORY "@PROJECT_SOURCE_DIR@"
	  OUTPUT_VARIABLE GIT_STATUS_OUTPUT
	)
	if (NOT GIT_STATUS_OUTPUT MATCHES "working directory clean")
		message(${GIT_STATUS_OUTPUT})
		message(FATAL_ERROR
		  "Source package must be created from a clean git working directory, "
		  "or you must specify -DSKIP_CHECK=1."
		)
	endif ()
endif ()

# Let git build the archive
message(STATUS "Creating ${PREFIX}.@Mapper_Source_FORMAT@")
execute_process(
  COMMAND "@GIT_EXECUTABLE@" archive
    --format "@Mapper_Source_FORMAT@"
    --prefix "${PREFIX}/"
    --output "${OUTPUT_DIR}/${ARCHIVE_NAME}.@Mapper_Source_FORMAT@"
    HEAD
  WORKING_DIRECTORY "@PROJECT_SOURCE_DIR@"
)
