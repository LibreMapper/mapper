# SPDX-License-Identifier: GPL-3.0-or-later
#
# Copyright 2017 Kai Pastor (OpenOrienteering)
#
# This file is part of LibreMapper.

file(GLOB input_files RELATIVE "@CMAKE_CURRENT_BINARY_DIR@" "@CMAKE_CURRENT_BINARY_DIR@/pdflatex/*.tex")
foreach(file ${input_files})
	file(READ "@CMAKE_CURRENT_BINARY_DIR@/${file}" input)
	
	# Turn internal typewriter-face hrefs into regular PDF hyperlinks
	string(REGEX REPLACE "\\\\href{#([^}]*)}{\\\\tt ([^}]*)}" "\\\\hyperlink{\\1}{\\2}" output "${input}")
	
	# Fix anchors in URLs, cf. https://suchideas.com/articles/computing/latex/errors/
	string(REGEX REPLACE "(\\\\href{[^}]*[^\\\\])#" "\\1\\\\#" output "${output}")
	
	string(MD5 input_md5 "${input}")
	string(MD5 output_md5 "${output}")
	if(NOT "${output_md5}" STREQUAL "${input_md5}")
		message(STATUS "Updating ${file}")
		file(WRITE "${file}" "${output}")
	endif()
endforeach()
